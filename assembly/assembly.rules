# -*- mode: Snakemake -*-
#
# Contig building and other assembly rules
#
# Requires IDBA_UD and AMOS.

Cfg['assembly'] = validate_paths(yaml.load(open('assembly/config.yml')))
ASSEMBLY_FP = Cfg['output_fp']/Cfg['assembly']['suffix']

rule _assemble_all:
    """Build contigs for all samples."""
    input:
        expand(
            str(ASSEMBLY_FP/'{sample}_assembly'/'minimo-contigs.fa'),
            sample=Samples.keys())

rule merge_fq:
    input:
        r1 = str(QC_FP/'decontam'/'{sample}_R1.fastq'),
        r2 = str(QC_FP/'decontam'/'{sample}_R2.fastq')
    output:
        str(ASSEMBLY_FP/'merged_reads'/'{sample}_merged.fasta')
    shell: "{Cfg[assembly][idba_fp]}/fq2fa --merge --filter {input.r1} {input.r2} {output}"


rule build_contigs:
    input:
        str(ASSEMBLY_FP/'merged_reads'/'{sample}_merged.fasta')
    output:
        str(ASSEMBLY_FP/'{sample}_assembly'/'contig.fa')
    log:
        str(ASSEMBLY_FP/'log'/'idba_ud'/'{sample}.out')
    params:
        out_fp = str(ASSEMBLY_FP/'{sample}_assembly')
    shell:
        """
        {Cfg[assembly][idba_fp]}/idba_ud -r {input} -o {params.out_fp} \
        --num_threads {Cfg[subcores]} --pre_correction &> {log} \
        """

rule minimo:
    input:
        str(ASSEMBLY_FP/'{sample}_assembly'/'contig.fa')
    output:
        block = temp(str(ASSEMBLY_FP/'{sample}_assembly'/'contig-contigs.fa')),
        unblock = str(ASSEMBLY_FP/'{sample}_assembly'/'minimo-contigs.fa')
    log:
        str(ASSEMBLY_FP/'log'/'minimo'/'{sample}.out')

    shell:
        """
        {Cfg[assembly][minimo_fp]} {input} -D FASTA_EXP=1 &> {log} && \
        perl unblock_fasta.pl {output.block} > {output.unblock}
        """

        
