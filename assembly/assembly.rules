# -*- mode: Snakemake -*-
#
# Contig building and other assembly rules
#
# Requires IDBA_UD and AMOS.

rule build_all:
    """Build contigs for all samples."""
    input:
        expand(
            str(ASSEMBLY_FP/'{sample}_assembly'/'minimo-contigs.fa'),
            sample=Samples.keys()
        )

rule merge_fq:
    input:
        r1 = str(QC_FP/'decontam'/'{sample}_R1.fastq'),
        r2 = str(QC_FP/'decontam'/'{sample}_R2.fastq')
    output:
        str(ASSEMBLY_FP/'merged_reads'/'{sample}_merged.fasta')
    shell: "fq2fa --merge --filter {input.r1} {input.r2} {output}"


rule build_contigs:
    input:
        str(ASSEMBLY_FP/'merged_reads'/'{sample}_merged.fasta')
    output:
        str(ASSEMBLY_FP/'{sample}_assembly'/'contig.fa')
    params:
        out_fp = str(ASSEMBLY_FP/'{sample}_assembly')
    shell:
        """
        {Cfg[idba_fp]} -r {input} -o {params.out_fp} \
        --num_threads {Cfg[subcores]} --pre_correction \
        """

rule minimo:
    input:
        str(ASSEMBLY_FP/'{sample}_assembly'/'contig.fa')
    output:
        block = temp(str(ASSEMBLY_FP/'{sample}_assembly'/'contig-contigs.fa')),
        unblock = str(ASSEMBLY_FP/'{sample}_assembly'/'minimo-contigs.fa')
    shell:
        """
        {Cfg[minimo_fp]} {input} -D FASTA_EXP=1 && \
        perl unblock_fasta.pl {output.block} > {output.unblock}
        """

        
